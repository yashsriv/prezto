#
# Executes commands at the start of an interactive session.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

[[ $- != *i* ]] && return

# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

# Prevent error on nomatch over ssh
unsetopt nomatch

# Rehash on installing a package
zstyle ':completion:*' rehash true

# history beginning search
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "$terminfo[kcuu1]" up-line-or-beginning-search
bindkey "$terminfo[kcud1]" down-line-or-beginning-search

# cd = pushd
setopt AUTO_PUSHD

# 2 escape is sudo
sudo-command-line() {
    [[ -z $BUFFER ]] && zle up-history
    if [[ $BUFFER == sudo\ * ]]; then
        LBUFFER="${LBUFFER#sudo }"
    elif [[ $BUFFER == $EDITOR\ * ]]; then
        LBUFFER="${LBUFFER#$EDITOR }"
        LBUFFER="sudoedit $LBUFFER"
    elif [[ $BUFFER == sudoedit\ * ]]; then
        LBUFFER="${LBUFFER#sudoedit }"
        LBUFFER="$EDITOR $LBUFFER"
    else
        LBUFFER="sudo $LBUFFER"
    fi
}
zle -N sudo-command-line
# Defined shortcut keys: [Esc] [Esc]
bindkey "^[^[" sudo-command-line

# Default editor for editing files over ssh and without
if [[ -n $SSH_CONNECTION ]]; then
  export EDITOR='vim'
fi

HISTSIZE=200000
SAVEHIST=10000

# PATHS
# Aliases

[ -f ~/.zsh.local ] && source ~/.zsh.local
# Execute previous command with sudo permissions
alias wtf='sudo $(history -p !!)'
# I accidentally type .. instead of cd ..
alias ..='cd ..'
# This is for finding out what is taking so much space on your drives!
alias diskspace='du -S | sort -n -r |more'
# Shortcut for git upload
alias gpom='git push origin master'
# IP Address ( actual and local)
alias ipa='echo -n "Global: " && dig +short myip.opendns.com @resolver1.opendns.com && echo -n " Local: " && ip route get 8.8.8.8 | head -1 | tr -s " " | cut -d " " -f7'
# Use python3 instead of python2
alias python='python3 '
# No spellcheck on rm
alias rm='nocorrect rm'
# Weather
alias weather='curl wttr.in/kanpur'
# Better cp
if (( $+commands[rsync] )); then
  alias cp='rsync -ah --progress'
fi

# Autostart in tmux
if (( $+commands[tmux] )); then
  case $(tty) in
    /dev/tty[0-9]*)
      ;;
    *)
      if [[ -z "$TMUX" && $TERM != "eterm-color" ]]; then
        tmux new-session && exit
      fi
      ;;
  esac
fi

# Startup Message
if (( $+commands[neofetch] )); then
  neofetch --image ascii
  echo ''
elif (( $+commands[screenfetch] )); then
  screenfetch
  echo ''
elif (( $+commands[archey] )); then
  archey
  echo ''
fi
